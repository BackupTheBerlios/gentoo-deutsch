<?xml version="1.0" encoding="iso-8859-1"?>

<xsl:stylesheet version="1.1" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">

<xsl:include href="../temp/config.xsl"/>
<xsl:include href="../temp/ebuild_new_org.xsl"/>
<xsl:include href="../temp/ebuild_all_de.xsl"/>

<xsl:output method="html" encoding="utf-8" indent="no"/>

<xsl:variable name="baseurl"><xsl:value-of select="$def_online"/>/</xsl:variable>
<xsl:variable name="styleurl"><xsl:value-of select="$baseurl"/><xsl:value-of select="$pub_style"/>/</xsl:variable>
<xsl:variable name="scripturl"><xsl:value-of select="$baseurl"/><xsl:value-of select="$pub_script"/>/</xsl:variable>
<xsl:variable name="imageurl"><xsl:value-of select="$baseurl"/><xsl:value-of select="$pub_images"/>/</xsl:variable>

<xsl:template match="guide|info|ebuilds|menu">
  <html>
  <head>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8"/>
    <title><xsl:value-of select="$var_title"/> @ Gentoo Linux - Das deutschprachige Portal</title>
    <link rel="stylesheet" type="text/css" href="{$styleurl}index.css" media="screen"/>
    <link rel="stylesheet" type="text/css" href="{$styleurl}print.css" media="print"/>    
    <link rel="shortcut icon" href="{$baseurl}favicon.ico" type="image/x-icon"/>
    <script language="javascript" type="text/javascript" src="{$scripturl}index.js"></script>
    <style type="text/css"><![CDATA[img { behavior: url("]]><xsl:value-of select="$scripturl"/>pngbehavior.htc<![CDATA["); }]]></style>
  </head>

  <body onclick="__body()">

  <xsl:call-template name="menu"/>
  
  <div id="logo">
    <a href="{$baseurl}/index.html" title="Zur Gentoo.de-Startseite">    
      <img src="{$imageurl}logo/logo.jpg" height="105" width="272" alt="Gentoo Linux Logo"/>
    </a>  
  </div>
  <div id="logobk">
    <img src="{$imageurl}logo/title.jpg" height="88" width="218" alt="Gentoo Linux - Das deutschsprachige Portal"/>
  </div>  

  <div id="about">
    <a href="{$baseurl}" class="icon">
      <img src="{$imageurl}icons/home.png" width="16" height="16" alt="Zurück zur Startseite"/>
      <xsl:value-of select="$lng_homepage"/>
    </a>
    <xsl:text>&#160; &#160;</xsl:text>
    <a href="javascript:window.print()" class="icon">
      <img src="{$imageurl}icons/print.png" width="16" height="16" alt="Diese Seite drucken"/>
      <xsl:value-of select="$lng_print"/>
    </a>
    
    <xsl:if test="/menu">
      <h4>Sitemap</h4>
      <p>
        Eine Sitemap ist eine strukturierte Übersicht einer Homepage. Dies ist neben der Suche
        eine der am häufigsten anzutreffenden navigationsunterstützenden Maßnahmen im Internet.
        Wir hoffen Ihre Suche verläuft erfolgreich :)
      </p>  
    </xsl:if>
    
    <xsl:if test="/ebuilds">
      <h4>Ebuilds</h4>
      <p>
        Hier sehen Sie alle vom Gentoo-Deutsch-Team erstellten bzw. geupdateten Pakete.
      </p>    
    </xsl:if>
		
    <xsl:if test="/guide">
      <h4><xsl:value-of select="$lng_version"/></h4>
      <p>
        <xsl:value-of select="version"/>
        <xsl:value-of select="$lng_date"/>
        <xsl:value-of select="date"/>
      </p>

      <h4><xsl:value-of select="$lng_author"/></h4>
      <p>
        <xsl:apply-templates select="author"/>
      </p>
    
      <h4><xsl:value-of select="$lng_overview"/></h4>
      <p>
        <xsl:apply-templates select="abstract"/>
      </p>
    
      <h4><xsl:value-of select="$lng_links"/></h4>
      <p>
        <xsl:for-each select="//uri">
          <xsl:variable name="uri">
            <xsl:choose>
              <xsl:when test="@link"><xsl:value-of select="@link"/></xsl:when>
              <xsl:otherwise><xsl:value-of select="."/></xsl:otherwise>
            </xsl:choose>
          </xsl:variable>

          <a href="{$uri}" target="_blank">
            <xsl:if test="position() &lt; 10 and count(//uri) &gt; 9">
	      <xsl:text>0</xsl:text>
            </xsl:if>  
          
            <xsl:value-of select="position()"/>
            <xsl:text>: </xsl:text>
	
            <xsl:variable name="myurl">
              <xsl:choose>
                <xsl:when test="contains($uri, '://')">
	          <xsl:value-of select="substring-after($uri, '://')"/>
	        </xsl:when>
	        <xsl:otherwise>
	          <xsl:value-of select="$uri"/>
                </xsl:otherwise>
              </xsl:choose>  
            </xsl:variable>
	
            <xsl:value-of select="substring($myurl, 0, 20)"/>
            <xsl:text>...</xsl:text>
          </a>
          <br/>
        </xsl:for-each>
      </p>
    </xsl:if>

    <xsl:apply-templates select="/*/box/*"/>
    
  </div>

  <div id="body">
    <h1 style="margin-top: 0px" class="top">
      <xsl:choose>
        <xsl:when test="/menu">
          <xsl:text>Übersicht</xsl:text>
        </xsl:when>
        <xsl:when test="/ebuilds">
          <xsl:text>Ebuild-Index</xsl:text>
        </xsl:when>
        <xsl:otherwise>
          <span style="font-size: 0.8em">&gt;&gt; </span>
          <xsl:value-of select="title"/>
        </xsl:otherwise>
      </xsl:choose>    
    </h1>
    
    <xsl:choose>
 
      <xsl:when test="/ebuild">
        <xsl:for-each select="*">
          <xsl:variable name="catname"><xsl:value-of select="name()"/></xsl:variable>
        
          <h1 class="info">
            <!--
            <span class="nr">
              <xsl:value-of select="position()"/>
            </span>
            <xsl:text> </xsl:text>
            -->
            <xsl:value-of select="$catname"/>
          </h1>
          <xsl:for-each select="entry">
            <p>
              <h2><xsl:value-of select="@name"/></h2>
              <xsl:value-of select="@desc"/>
              <br/>
              <b>Ebuild: </b><a class="uri" href="{$siteurl}/../Daten/_alt/ebuilds/{$catname}/{@name}/{@ebuild}"><xsl:value-of select="@ebuild"/></a>
            </p>          
          </xsl:for-each>
        </xsl:for-each>
      </xsl:when>
      <xsl:when test="/menu">
        <table width="100%" cellpadding="0" cellspacing="0" class="noborder" style="font-size: 1.2em">
        <tr valign="top">
        <xsl:variable name="width"><xsl:value-of select="100 div count(item)"/></xsl:variable>
        
        <xsl:for-each select="item">
          <xsl:variable name="url_1"><xsl:value-of select="@href"/></xsl:variable>
        
          <td width="{$width}%">
            <table width="100%" cellpadding="0" cellspacing="0">
              <tr>
                <td class="sitemain">
                  <a href="{$siteurl}/{$url_1}/index.html" class="sitemain">
                    <xsl:value-of select="@title"/>
                  </a>
                </td>
              </tr>
              <tr>
                <td>
                <ul class="sitelist">
                <xsl:for-each select="item">
                  <xsl:variable name="url_2"><xsl:value-of select="@href"/></xsl:variable>              
                  
                  <li class="sitelist"> 
                    <a href="{$siteurl}/{$url_1}/{$url_2}/index.html" class="sitesub">
                      <xsl:value-of select="@title"/>
                    </a>

                    <xsl:if test="item"> 
                      <ul class="sitelist">
                        <xsl:for-each select="item">
                          <xsl:variable name="url_3"><xsl:value-of select="@href"/></xsl:variable>                                            
                      
                          <li class="sitelist">
                            <a href="{$siteurl}/{$url_1}/{$url_2}/{$url_3}/index.html" class="sitesub">
                              <xsl:value-of select="@title"/>
                            </a>
                          </li>
                        </xsl:for-each>
                      </ul>
                    </xsl:if>
                  </li>  
                </xsl:for-each>                
                </ul>
                </td>
              </tr>
            </table>
          </td>  
        </xsl:for-each>
        </tr>
        </table>  
      </xsl:when>
      <xsl:otherwise>
        <xsl:apply-templates select="chapter"/>
      </xsl:otherwise>
    </xsl:choose>  
      
      
    <br/><br/>
  </div>
  
  <script language="javascript"><![CDATA[var browser = new __browser(); var ctl = new __ctl(); __init()]]></script>
  
  </body>
  </html>
</xsl:template>

<xsl:template match="author">
  <xsl:choose>
    <xsl:when test="mail">
      <a href="mailto:{mail/@link}" title="{@title}">
        <xsl:value-of select="mail"/>
      </a>
    </xsl:when>
    <xsl:otherwise>
      <span title="{@title}">
        <xsl:value-of select="."/>
      </span>
    </xsl:otherwise>
  </xsl:choose>
  <br/>
</xsl:template>

<!-- Block inside Box -->
<xsl:template match="block">
  <h4><xsl:value-of select="title"/></h4>
  <xsl:apply-templates select="p"/>	
</xsl:template>

<xsl:template match="chapter">
  <xsl:variable name="cid"><xsl:number/>.</xsl:variable>
  
  <xsl:if test="title">
    <h1>
      <xsl:if test="/guide">
        <span class="nr"><xsl:value-of select="$cid"/></span>
        <xsl:text> </xsl:text>
      </xsl:if>

      <xsl:if test="/info">
        <xsl:attribute name="class">info</xsl:attribute>
      </xsl:if>

      <xsl:value-of select="title"/>
    </h1>
  </xsl:if>
  
  <xsl:for-each select="section">
    <xsl:variable name="sid">
      <xsl:value-of select="position()"/>
    </xsl:variable>
    
    <xsl:if test="title">
      <h2>
        <xsl:if test="/guide">
          <span class="nr"><xsl:value-of select="$cid"/><xsl:value-of select="$sid"/></span>
          <xsl:text> </xsl:text>
        </xsl:if>

        <xsl:value-of select="title"/>
      </h2>
    </xsl:if>
    
    <xsl:apply-templates select="body"/>
  </xsl:for-each>  
</xsl:template>

<xsl:template match="p">
  <p><xsl:apply-templates/></p>
</xsl:template>

<xsl:template match="table">
  <table cellpadding="0" cellspacing="0" border="0">
  <xsl:for-each select="tr">
    <tr>
      <xsl:if test="position() mod 2 != 0">
        <xsl:attribute name="class">high</xsl:attribute>
      </xsl:if>
      <xsl:apply-templates/>
    </tr>
  </xsl:for-each>
  </table>
</xsl:template>

<xsl:template match="tr">
  <xsl:apply-templates/>
</xsl:template>

<xsl:template match="th">
  <td class="head" nowrap="nowrap"><xsl:apply-templates/></td>
</xsl:template>

<xsl:template match="ti">
  <td><xsl:apply-templates/></td>
</xsl:template>

<xsl:template match="path">
  <span class="path"><xsl:apply-templates/></span>
</xsl:template>

<xsl:template match="quot">
  <span class="quot">&#187;<xsl:apply-templates/>&#171;</span>
</xsl:template>

<xsl:template match="uri">
  <a class="uri">
    <xsl:attribute name="href">
      <xsl:choose>
        <xsl:when test="@link"><xsl:value-of select="@link"/></xsl:when>
        <xsl:otherwise><xsl:value-of select="."/></xsl:otherwise>
      </xsl:choose>
    </xsl:attribute>
    <xsl:apply-templates/>
  </a>
</xsl:template>

<xsl:template match="mail">
  <a class="uri">
    <xsl:attribute name="href">
      <xsl:text>mailto:</xsl:text>
      <xsl:choose>
	<xsl:when test="@nospam and @link">NOSPAM-<xsl:value-of select="@link"/></xsl:when>
        <xsl:when test="@link"><xsl:value-of select="@link"/></xsl:when>                                                            
        <xsl:otherwise><xsl:value-of select="."/></xsl:otherwise>   
      </xsl:choose>
    </xsl:attribute>
    <xsl:apply-templates/>
  </a>
</xsl:template>

<xsl:template match="comment">
  <span class="comment"><xsl:apply-templates/></span>
</xsl:template>

<xsl:template match="codenote">
  <span class="comment">// <xsl:value-of select="."/></span>
</xsl:template>

<xsl:template match="note">
  <table class="note" cellpadding="0" cellspacing="0" border="0">
    <tr>
      <td class="header"><xsl:value-of select="$lng_note"/></td>
    </tr>
    <tr>
      <td class="content">
        <xsl:apply-templates/>
      </td>
    </tr>
  </table>
</xsl:template>

<xsl:template match="impo">
  <table class="impo" cellpadding="0" cellspacing="0" border="0">
    <tr>
      <td class="header"><xsl:value-of select="$lng_impo"/></td>
    </tr>
    <tr>
      <td class="content">
        <xsl:apply-templates/>
      </td>
    </tr>
  </table>
</xsl:template>

<xsl:template match="warn">
  <table class="warn" cellpadding="0" cellspacing="0" border="0">
    <tr>
      <td class="header"><xsl:value-of select="$lng_warn"/></td>
    </tr>
    <tr>
      <td class="content">
        <xsl:apply-templates/>
      </td>
    </tr>
  </table>
</xsl:template>

<xsl:template match="box">
  <table>
    <tr>
      <td>
        <xsl:apply-templates/>
      </td>
    </tr>
  </table>
</xsl:template>

<xsl:template match="pre">
  <xsl:variable name="prenum"><xsl:number level="any"/></xsl:variable>
  <xsl:variable name="preid">doc_pre<xsl:number level="any"/></xsl:variable>
  <a name="{$preid}"/>
  
  <div class="codetitle">
    <xsl:text>Befehlsauflistung </xsl:text>
    <xsl:value-of select="$prenum"/>

    <xsl:if test="@caption">
      <xsl:text>: </xsl:text>
      <span style="font-weight: normal"><xsl:value-of select="@caption"/></span>
    </xsl:if>
  </div>
  <pre><xsl:apply-templates/></pre>
</xsl:template>

<xsl:template match="c">
  <span class="code"><xsl:apply-templates/></span>
</xsl:template>

<xsl:template match="b">
  <b><xsl:apply-templates/></b>
</xsl:template>

<xsl:template match="i">
  <span class="input"><xsl:apply-templates/></span>
</xsl:template>

<xsl:template match="brite">
  <span class="brite"><xsl:apply-templates/></span>
</xsl:template>

<xsl:template match="br">
  <br/>
</xsl:template>

<xsl:template match="ul">
  <ul><xsl:apply-templates/></ul>
</xsl:template>

<xsl:template match="li">
  <li><xsl:apply-templates/></li>
</xsl:template>


</xsl:stylesheet>
